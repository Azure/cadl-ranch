import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-ranch-expect";

using Cadl.Http;
using Azure.Core.Operations;

@serviceTitle("ModelCollectionPropertiesClient")
@serviceVersion("1.0.0")
@server("http://localhost:3000", "TestServer endpoint")
@doc("Illustrates input, output, and round-trip models that have properties that are collections of models.")
@route("/collection-models")
namespace Cadl.TestServer.ModelCollectionProperties;

@doc("Simple model that will appear in a collection.")
model SimpleModel {
  @doc("Required string.")
  requiredString: string;

  @doc("Required int.")
  requiredInt: int32;
}

@doc("Simple model with model collection properties")
model ModelCollectionModel {
  @doc("Required collection of models.")
  requiredModelCollection: SimpleModel[];

  @doc("Optional collection of models.")
  optionalModelCollection?: SimpleModel[];
}

@scenario
@scenarioDoc("Generate and send input model with model collection properties.")
@route("/models")
@post
op sendCollectionModel(@body input: ModelCollectionModel): Response<200> | ErrorResponse;

@scenario
@scenarioDoc("Generate and receive output model with model collection properties.")
@route("/models")
@get
op getCollectionModel(): OkResponse<ModelCollectionModel> | ErrorResponse;

@scenario
@scenarioDoc("Generate, send, and receive round-trip model with model collection properties.")
@route("/models")
@put
op setCollectionModel(@body input: ModelCollectionModel): ModelCollectionModel | ErrorResponse;
