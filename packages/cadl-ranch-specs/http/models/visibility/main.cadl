
import "@cadl-lang/rest";
import "@azure-tools/cadl-ranch-expect";


using Cadl.Http;

@doc("Illustrates models with visibility properties.")
@scenarioService("/models/visibility")
namespace Models.Visibility;

@doc("Output model with visibility properties.")
model VisibilityModel {
  @doc("Required string, illustrating a readonly property.")
  @visibility("read")
  requiredReadonlyString: string;

  @doc("Required int32, illustrating a query property.")
  @visibility("query")
  requiredQueryInt32: int32;

  @doc("Required string[], illustrating a create property.")
  @visibility("create")
  requiredCreateStringList: string[];

  @doc("Required int32[], illustrating a update property.")
  @visibility("update")
  requiredUpdateIntList: int32[];

  @doc("Required bool, illustrating a delete property.")
  @visibility("delete")
  requiredDeleteBoolean: boolean;
}

@scenario
@scenarioDoc("""
Generate and receive output model with readonly properties.
Expected input body:
```json
{
  requiredQueryInt32: 123,
}
```

Expected response body:
```json
{
  requiredReadonlyString: "abc",
}
```
""")
@get
op getModel(@body input: VisibilityModel): VisibilityModel;

@scenario
@scenarioDoc("""
Generate abd send put model with write/create properties.
Expected input body:
```json
{
  requiredQueryInt32: 123,
}
```
""")
@head
op headModel(@body input: VisibilityModel): OkResponse;


@scenario
@scenarioDoc("""
Generate abd send put model with write/create/udpate properties.
Expected input body:
```json
{
  requiredCreateStringList: ["foo", "bar"],
  requiredUpdateIntList: [1, 2],
}
```
""")
@put
op putModel(@body input: VisibilityModel): void;

@scenario
@scenarioDoc("""
Generate abd send put model with write/udpate properties.
Expected input body:
```json
{
  requiredUpdateIntList: [1, 2],
}
```
""")
@patch
op patchModel(@body input: VisibilityModel): void;

@scenario
@scenarioDoc("""
Generate abd send put model with write/create properties.
Expected input body:
```json
{
  requiredCreateStringList: ["foo", "bar"],
}
```
""")
@post
op postModel(@body input: VisibilityModel): void;

@scenario
@scenarioDoc("""
Generate abd send put model with write/create properties.
Expected input body:
```json
{
  requiredDeleteBoolean: true,
}
```
""")
@delete
op deleteModel(@body input: VisibilityModel): void;
