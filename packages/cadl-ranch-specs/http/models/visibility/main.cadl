import "@cadl-lang/rest";
import "@azure-tools/cadl-ranch-expect";

using Cadl.Http;

@doc("Illustrates models with visibility properties.")
@scenarioService("/models/visibility")
namespace Models.Visibility;

@doc("Output model with visibility properties.")
model VisibilityModel {
  @doc("Required string, illustrating a readonly value type property.")
  @visibility("read")
  requiredReadonlyString: string;

  @doc("Required int, illustrating a writeonly value type property.")
  @visibility("write")
  requiredWriteonlyInt: int32;

  @doc("Required createonly string collection.")
  @visibility("create")
  requiredCreateonlyStringList: string[];

  @doc("Required updateonly int collection.")
  @visibility("update")
  requiredUpdateonlyIntList: int32[];
}

@scenario
@scenarioDoc("""
Generate and receive output model with readonly properties.
Expected response body:
```json
{
  requiredReadonlyString: "abc",
}
```
""")
@get
op getModel(): VisibilityModel;

@scenario
@scenarioDoc("""
Generate abd send put model with write/create/udpate properties.
Expected input body:
```json
{
  requiredWriteonlyInt: 123,
  requiredCreateonlyStringList: ["foo", "bar"],
  requiredUpdateonlyIntList: [1, 2],
}
```
""")
@put
op putModel(@body input: VisibilityModel): void;

@scenario
@scenarioDoc("""
Generate abd send put model with write/udpate properties.
Expected input body:
```json
{
  requiredWriteonlyInt: 123,
  requiredUpdateonlyIntList: [1, 2],
}
```
""")
@patch
op patchModel(@body input: VisibilityModel): void;

@scenario
@scenarioDoc("""
Generate abd send put model with write/create properties.
Expected input body:
```json
{
  requiredWriteonlyInt: 123,
  requiredCreateonlyStringList: ["foo", "bar"],
}
```
""")
@post
op postModel(@body input: VisibilityModel): void;
