import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-ranch-expect";

using Cadl.Http;
using Azure.Core.Foundations;

@doc("Illustrates input, output, and round-trip models with required enum type properties.")
@scenarioService("/enum-properties-basic")
namespace Model.EnumProperties;

@doc("The language to translate the input text into.")
@knownValues(TranslationLanguageValues)
model TranslationLanguage is string;

@doc("The supported languages to translate input text into.")
enum TranslationLanguageValues {
  @doc("Translate to English")
  English,

  @doc("Translate to Spanish")
  Spanish,

  @doc("Translate to Mandarin")
  Mandarin,
  // Value without @doc decorator, to test this case.
  Undocumented,
}

@doc("Represents the days of the week using a standard, non-string enum.")
enum DayOfTheWeek {
  Monday,
  Tuesday,

  @doc("Wednesday is documented to test this case.")
  Wednesday,
  Thursday,
  Friday,
  Saturday,
  Sunday,
}

@doc("Input model with enum properties.")
model InputModel {
  @doc("Required standard enum value.")
  Day: DayOfTheWeek;

  @doc("Required string enum value.")
  Language: TranslationLanguage;
}

@doc("Output model with enum properties")
model OutputModel {
  @doc("Required standard enum value.")
  Day: DayOfTheWeek;

  @doc("Required string enum value.")
  Language: TranslationLanguage;
}

@doc("Round-trip model with enum properties")
model RoundTripModel {
  @doc("Required standard enum value.")
  Day: DayOfTheWeek;

  @doc("Required string enum value.")
  Language: TranslationLanguage;
}

@scenario
@scenarioDoc("""
Generate and send input model with required enum properties.
expected requestBody = { Day: "Wednesday", Language: "Undocumented" }
""")
@route("/models")
@post
op sendEnumPropertyModel(@body input: InputModel): Response<200> | ErrorResponse;

@scenario
@scenarioDoc("""
Generate and receive output model with required enum properties.
expected responseBody = { Day: "Wednesday", Language: "Undocumented" }
""")
@route("/models")
@get
op getEnumPropertyModel(): OutputModel | ErrorResponse;

@scenario
@scenarioDoc("""
Generate, send, and receive round-trip model with required enum properties.
expected requestBody = { Day: "Wednesday", Language: "Undocumented" }
expected responseBody = { Day: "Monday", Language: "English" }
""")
@route("/models")
@put
op setEnumPropertyModel(@body input: RoundTripModel): RoundTripModel | ErrorResponse;
