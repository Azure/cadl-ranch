import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-ranch-expect";

using Cadl.Http;
using Azure.Core.Foundations;

@serviceTitle("ModelDictionaryPropertiesClient")
@serviceVersion("1.0.0")
@server("http://localhost:3000", "TestServer endpoint")
@doc("Illustrates input, output, and round-trip models that have properties that are dictionary of models.")
@route("/model-dictionary-properties")
namespace Cadl.TestServer.ModelDictionaryProperties;

@doc("Simple model that will appear in a dictionary.")
model SimpleModel {
  @doc("Required string.")
  requiredString: string;
}

@doc("Simple model with model collection properties")
model ModelDictionaryModel {
  @doc("Optional collection of models.")
  optionalModelDictionary?: Record<SimpleModel>;
}

@scenario
@scenarioDoc("Generate and send input model with dictionary properties. The valid value is {optionalModelDictionary: {key: {requiredString: \"abc\"}}}")
@route("/models")
@post
op sendDictionaryModel(@body input: ModelDictionaryModel): OkResponse | ErrorResponse;

@scenario
@scenarioDoc("Generate and receive output model with dictionary properties. The return value is {optionalModelDictionary: {key: {requiredString: \"abc\"}}}")
@route("/models")
@get
op getDictionaryModel(): ModelDictionaryModel | ErrorResponse;

@scenario
@scenarioDoc("Generate, send, and receive round-trip model with dictionary properties.")
@route("/models")
@put
op setDictionaryModel(@body input: ModelDictionaryModel): ModelDictionaryModel | ErrorResponse;
