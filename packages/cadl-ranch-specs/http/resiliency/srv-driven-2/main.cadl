import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-ranch-expect";

using Cadl.Versioning;

@versioned(Versions)
@serviceTitle("DPGClient")
@serviceVersion("1.1.0")
@server("http://localhost:3000", "Testserver endpoint")
@doc("DPG Swagger, this is the initial swagger a service could do")
@Cadl.Rest.produces("application/json")
@Cadl.Rest.consumes("application/json")
namespace Cadl.Testserver;
using Cadl.Http;

enum Versions {
  v10: "1.0.0",
  v11: "1.1.0",
}

model PostInput {
  url: string;
}

model Message {
  message: string;
}

@doc("Answer from service")
model MessageResponse {
  @statusCode statusCode: 200;
  @body body: Message;
}

@route("/serviceDriven")
interface params {
  @scenario
  @scenarioDoc("""
Show that you can call a HEAD HTTP endpoint.
This test has grow to a new optional parameter, and the generated code should be backward compatible with srv-driven-1.
The value you pass for the parameter is not verified by the mock server.
""")
  @route("/parameters")
  @doc("Head request, no params.\n Initially has no query parameters. After evolution, a new optional query parameter is added")
  @head
  headNoParams(
    @added(Versions.v11)
    @doc("I'm a new input optional parameter")
    @query
    new_parameter?: string
  ): {
    @statusCode statusCode: 200;
  };

  @scenario
  @scenarioDoc("""
Show that you can call a GET HTTP endpoint.
This test has grow to a new optional parameter, and the generated code should be backward compatible with srv-driven-1.
The value you pass for the parameter is not verified by the mock server.
""")
  @route("/parameters")
  @doc("Get true Boolean value on path.\n Initially only has one required Query Parameter. After evolution, a new optional query parameter is added")
  @get
  getRequired(
    @doc("I am a required parameter")
    @query
    parameter: string,

    @added(Versions.v11)
    @doc("I'm a new input optional parameter")
    @query
    new_parameter?: string
  ): MessageResponse;

  @scenario
  @scenarioDoc("""
Show that you can call a PUT HTTP endpoint.
This test has grow to a new optional parameter, and the generated code should be backward compatible with srv-driven-1.
The value you pass for the parameter is not verified by the mock server.
""")
  @route("/parameters")
  @doc("Initially has one required query parameter and one optional query parameter.  After evolution, a new optional query parameter is added")
  @put
  putRequiredOptional(
    @doc("I am a required parameter")
    @query
    requiredParam: string,

    @doc("I am an optional parameter")
    @query
    optionalParam?: string,

    @added(Versions.v11)
    @doc("I'm a new input optional parameter")
    @query
    new_parameter?: string
  ): MessageResponse;

  // In the updated version, I can take "image/jpeg" as input as well
  @scenario
  @scenarioDoc("""
Show that you can call a POST HTTP endpoint.
This test now accept both image/jpeg and applicat/json and is expected keeping backward compat with srv-driven-1.
Pass the JSON: `{"url": "http://example.org/myimage.jpeg"}` or a binary with content-type image/jpeg. The server do not check the binary.
""")
  @route("/parameters/{contenttype}")
  @doc("POST a JSON or a JPEG")
  @post
  postParameters(
    @path contenttype: "json" | "jpeg",
    @doc("I am a body parameter. My only valid JSON entry is { url: \"http://example.org/myimage.jpeg\" }")
    @body
    parameter: PostInput
  ): MessageResponse;

  @scenario
  @scenarioDoc("""
  Show that you can support a new method in the same operation group.
  """)
  @added(Versions.v11)
  @route("/parameters")
  @doc("Delete something.\n Initially the path exists but there is no delete method. After evolution this is a new method in a known path")
  @delete
  deleteParameters(): {
    @statusCode statusCode: 204;
  };

  @scenario
  @scenarioDoc("""
Show that you can call a GET HTTP endpoint.
This version has his main parameter optional first, making the grow-up story to two optionals.
The value you pass for the parameter is not verified by the mock server.
""")
  @route("/moreParameters")
  @doc("Get true Boolean value on path.\n Initially has one optional query parameter. After evolution, a new optional query parameter is added")
  @get
  getOptional(
    @doc("I am an optional parameter")
    @query
    optionalParam?: string,

    @added(Versions.v11)
    @doc("I'm a new input optional parameter")
    @query
    new_parameter?: string
  ): MessageResponse;

  @scenario
  @scenarioDoc("""
Show that you can call a GET HTTP endpoint.
This is a totally new operation in this API version.
""")
  @added(Versions.v11)
  @route("/newPath")
  @doc("I'm a new operation.\n Initiallty neither path or method exist for this operation. After evolution, this is a new method in a new path")
  @get
  getNewOperation(): MessageResponse;
}
