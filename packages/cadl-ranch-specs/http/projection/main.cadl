import "@cadl-lang/rest";
import "@azure-tools/cadl-ranch-expect";

using Cadl.Http;

@doc("Projection")
@supportedBy("dpg")
@scenarioService("/projection")
namespace Projection;

model Project {
  @projectedName("json", "codegen")
  producedBy?: string;

  @projectedName("client", "createdBy")
  builtfrom?: string;

  @projectedName("client", "DontUseMeAsName") // Should be ignored
  @projectedName("csharp", "MadeFor")
  @projectedName("java", "MadeFor")
  @projectedName("javascript", "MadeFor")
  @projectedName("python", "made_for")
  wasMadeFor?: string;
}

@scenario
@scenarioDoc("""
CADL name is SDK, Projection is JSON name. Send:
```json
{"codegen": "DPG"}
```
""")
@route("/json")
@post
op jsonProjection(...Project): void;

@scenario
@scenarioDoc("""
CADL name is JSON, Projection is client name. Send:
```json
{"builtfrom": "DPG"}
```
""")
@route("/client")
@post
op clientProjection(...Project): void;

@scenario
@scenarioDoc("""
CADL name is JSON, Projection is client name per language override. Send:
```json
{"wasMadeFor": "customers"}
```
""")
@route("/language")
@post
op languageProjection(...Project): void;

// May add query/header project after https://github.com/microsoft/cadl/issues/1487
